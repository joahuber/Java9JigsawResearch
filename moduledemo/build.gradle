subprojects {
	group 'ch.bbv'
	version '1.0-SNAPSHOT'

	apply plugin: 'java'

	sourceCompatibility = 1.8

	repositories {
	    mavenCentral()
	}

	configurations {
	    classycle
	    jdepend
	}

	dependencies {
	    compile 'com.google.inject:guice:4.0'
	    testCompile group: 'junit', name: 'junit', version: '4.11'
	    classycle 'org.specs2:classycle:1.4.3'
	    jdepend 'jdepend:jdepend:2.9.1'
	}

	task classycleReport(type: JavaExec, dependsOn: 'build') {
	    classpath = configurations.classycle
	    main = 'classycle.Analyser'
	    args '-xmlFile=classycleReport/classycle.xml', '-title=CarWash', 'build/classes/main'
	}

	task classycleCheck(type: JavaExec, dependsOn: 'build') {
	    classpath = configurations.classycle
	    main = 'classycle.dependency.DependencyChecker'
	    args '-dependencies=@../classycle-conf.ddf', 'build/classes/main'
	}

	task jdependUI(type: JavaExec, dependsOn: 'build') {
	    classpath = configurations.jdepend
	    main = 'jdepend.swingui.JDepend'
	    args 'build/classes/main'
	}

	task jdependText(type: JavaExec, dependsOn: 'build') {
	    classpath = configurations.jdepend
	    main = 'jdepend.textui.JDepend'
	    args 'build/classes/main'
	}
}
